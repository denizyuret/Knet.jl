{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "source": [
    "# Iterators\n",
    "(c) Deniz Yuret, 2019\n",
    "\n",
    "* Objective: Learning how to construct and use Julia iterators.\n",
    "* Reading: [Interfaces](https://docs.julialang.org/en/v1/manual/interfaces/#man-interface-iteration-1),  [Collections](https://docs.julialang.org/en/v1/base/collections/#lib-collections-iteration-1), [Iteration Utilities](https://docs.julialang.org/en/v1/base/iterators) and [Generator expressions](https://docs.julialang.org/en/v1/manual/arrays/#Generator-Expressions-1) in the Julia manual.\n",
    "* Prerequisites: [minibatch, Data](https://github.com/denizyuret/Knet.jl/blob/master/src/data.jl) from the [MNIST notebook](20.mnist.ipynb)\n",
    "* New functions: \n",
    "[first](https://docs.julialang.org/en/v1/base/collections/#Base.first), \n",
    "[collect](https://docs.julialang.org/en/v1/base/collections/#Base.collect-Tuple{Any}), \n",
    "[repeat](https://docs.julialang.org/en/v1/base/arrays/#Base.repeat), \n",
    "[take](https://docs.julialang.org/en/v1/base/iterators/#Base.Iterators.take), \n",
    "[drop](https://docs.julialang.org/en/v1/base/iterators/#Base.Iterators.drop), \n",
    "[cycle](https://docs.julialang.org/en/v1/base/iterators/#Base.Iterators.cycle), \n",
    "[Stateful](https://docs.julialang.org/en/v1/base/iterators/#Base.Iterators.Stateful), \n",
    "[iterate](https://docs.julialang.org/en/v1/base/collections/#lib-collections-iteration-1)\n",
    "\n",
    "The `minibatch` function returns a `Knet.Data` object implemented as a Julia iterator that generates (x,y) minibatches. Iterators are lazy objects that only generate their next element when asked. This has the advantage of not wasting time and memory trying to create and store all the elements at once. We can even have infinite iterators! The training algorithms in Knet are also implemented as iterators so that:\n",
    "1. We can monitor and report the training loss\n",
    "2. We can take snapshots of the model during training\n",
    "3. We can pause/terminate training when necessary\n",
    "\n",
    "Here are some things Julia can do with iterators:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [],
   "source": [
    "# Set display width, load packages, import symbols\n",
    "ENV[\"COLUMNS\"]=72\n",
    "using Pkg; haskey(Pkg.installed(),\"Knet\") || Pkg.add(\"Knet\")\n",
    "using Base.Iterators: take, drop, cycle, Stateful\n",
    "using Knet"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "┌ Info: Loading MNIST...\n",
      "└ @ Main /home/deniz/.julia/dev/Knet/data/mnist.jl:33\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Knet.Data{Tuple{Array{Float32,4},Array{UInt8,1}}}(Float32[0.0 0.0 … 0.0 0.0; 0.0 0.0 … 0.0 0.0; … ; 0.0 0.0 … 0.0 0.0; 0.0 0.0 … 0.0 0.0], UInt8[0x07 0x02 … 0x05 0x06], 100, 10000, false, 9901, 1:10000, false, (28, 28, 1, 10000), (10000,), Array{Float32,4}, Array{UInt8,1})"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Load data\n",
    "include(Knet.dir(\"data\",\"mnist.jl\"))\n",
    "xtrn,ytrn,xtst,ytst = mnist()\n",
    "dtst = minibatch(xtst,ytst,100)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(\"28×28×1×100 Array{Float32,4}\", \"100-element Array{UInt8,1}\")"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# We can peek at the first element using first()\n",
    "summary.(first(dtst))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 100\n"
     ]
    }
   ],
   "source": [
    "# Iterators can be used in for loops\n",
    "# Let's count the elements in dtst:\n",
    "n = 0\n",
    "for (x,y) in dtst; n += 1; end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"100-element Array{Tuple{Array{Float32,4},Array{UInt8,1}},1}\""
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Iterators can be converted to arrays using `collect` \n",
    "# (don't do this unless necessary, it just wastes memory. Use a for loop instead)\n",
    "collect(dtst) |> summary"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 500\n"
     ]
    }
   ],
   "source": [
    "# We can generate an iterator for multiple epochs using `repeat`\n",
    "# (an epoch is a single pass over the dataset)\n",
    "n = 0\n",
    "for (x,y) in repeat(dtst,5); n += 1; end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 20\n"
     ]
    }
   ],
   "source": [
    "# We can generate partial epochs using `take` which takes the first n elements\n",
    "n = 0\n",
    "for (x,y) in take(dtst,20); n += 1; end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 80\n"
     ]
    }
   ],
   "source": [
    "# We can also generate partial epochs using `drop` which drops the first n elements\n",
    "n = 0\n",
    "for (x,y) in drop(dtst,20); n += 1; end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 32502\n"
     ]
    }
   ],
   "source": [
    "# We can repeat forever using `cycle` (this is useful to train until convergence)\n",
    "# You do not want to collect a cycle or run a for loop without break! \n",
    "n = 0; t = time_ns()\n",
    "for (x,y) in cycle(dtst)\n",
    "    n += 1\n",
    "    time_ns() - t > 2e9 && break # Break after 2 seconds\n",
    "end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[7, 2, 1, 10, 4]\n",
      "[7, 2, 1, 10, 4]\n",
      "[7, 2, 1, 10, 4]\n",
      "[6, 10, 5, 4, 9]\n"
     ]
    }
   ],
   "source": [
    "# We can make an iterator `Stateful` so it remembers where it left off.\n",
    "# (by default iterators start from the beginning)\n",
    "dtst1 = dtst            # dtst1 will start from beginning every time\n",
    "dtst2 = Stateful(dtst)  # dtst2 will remember where we left off\n",
    "for (x,y) in dtst1; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst1; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst2; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst2; println(Int.(y[1:5])); break; end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[7, 2, 1, 10, 4]\n",
      "[7, 2, 1, 10, 4]\n",
      "[6, 9, 2, 2, 6]\n",
      "[7, 4, 9, 9, 9]\n"
     ]
    }
   ],
   "source": [
    "# We can shuffle instances at every epoch using the keyword argument `shuffle=true`\n",
    "# (by default elements are generated in the same order)\n",
    "dtst1 = minibatch(xtst,ytst,100)              # dtst1 iterates in the same order\n",
    "dtst2 = minibatch(xtst,ytst,100,shuffle=true) # dtst2 shuffles each time\n",
    "for (x,y) in dtst1; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst1; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst2; println(Int.(y[1:5])); break; end\n",
    "for (x,y) in dtst2; println(Int.(y[1:5])); break; end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1×100 LinearAlgebra.Adjoint{Float32,Array{Float32,1}}:\n",
       " 7990.35  7842.33  8162.68  7692.77  …  8494.0  7361.33  8643.01"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# We can construct new iterators using [Generator expressions](https://docs.julialang.org/en/v1/manual/arrays/#Generator-Expressions-1)\n",
    "# The following example constructs an iterator over the x norms in a dataset:\n",
    "xnorm(data) = (sum(abs2,x) for (x,y) in data)\n",
    "collect(xnorm(dtst))'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n = 100\n"
     ]
    }
   ],
   "source": [
    "# Every iterator implements the `iterate` function which returns\n",
    "# the next element and state (or nothing if no elements left).\n",
    "# Here is how the for loop for dtst is implemented:\n",
    "n = 0; next = iterate(dtst)\n",
    "while next != nothing\n",
    "    ((x,y), state) = next\n",
    "    n += 1\n",
    "    next = iterate(dtst,state)\n",
    "end\n",
    "@show n;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1×100 LinearAlgebra.Adjoint{Any,Array{Any,1}}:\n",
       " 7990.35  7842.33  8162.68  7692.77  …  8494.0  7361.33  8643.01"
      ]
     },
     "execution_count": 14,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# You can define your own iterator by declaring a new type and overriding the `iterate` method.\n",
    "# Here is another way to define an iterator over the x norms in a dataset:\n",
    "struct Xnorm; itr; end\n",
    "\n",
    "function Base.iterate(f::Xnorm, s...)\n",
    "    next = iterate(f.itr, s...)\n",
    "    next === nothing && return nothing\n",
    "    ((x,y),state) = next\n",
    "    return sum(abs2,x), state\n",
    "end\n",
    "\n",
    "Base.length(f::Xnorm) = length(f.itr) # collect needs this\n",
    "\n",
    "collect(Xnorm(dtst))'"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.0.3",
   "language": "julia",
   "name": "julia-1.0"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.0.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
